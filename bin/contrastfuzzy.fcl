// Block definition (there may be more than one block per file)
FUNCTION_BLOCK gscontrast

// Define input variables
VAR_INPUT
    inpixel : REAL;
END_VAR

// Define output variables
VAR_OUTPUT
    outpixel : REAL;
END_VAR

// Fuzzify input variable 'inpixel'
FUZZIFY inpixel
    TERM dark := (0, 1) (70, 1) (127, 0); 
    TERM grey := (70, 0) (127, 1) (184, 0);
    TERM bright := (127, 0) (184, 1) (255, 1);
END_FUZZIFY

// Defuzzify output variable 'outpixel'
DEFUZZIFY outpixel
    TERM darker := (-1, 0) (0, 1) (1, 0);
    TERM grey := (126, 0) (127, 1) (128, 0);
    TERM brighter := (254, 0) (255, 1) (256, 0);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF inpixel IS dark THEN outpixel IS darker;

    RULE 2 : IF inpixel IS grey THEN outpixel IS grey; 

    RULE 3 : IF inpixel IS bright THEN outpixel IS brighter;
END_RULEBLOCK

END_FUNCTION_BLOCK